# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build_overrides/build.gni")

if (current_toolchain == host_toolchain) {
  executable("cddl") {
    sources = [
      "codegen.cc",
      "codegen.h",
      "logging.cc",
      "logging.h",
      "main.cc",
      "parse.cc",
      "parse.h",
      "sema.cc",
      "sema.h",
    ]

    deps = [
      "../../third_party/abseil:abseil_always_host_toolchain",
    ]

    # As this executable is ran on the host machine, we shouldn't
    # use the runtime libraries for cross compilation.
    if (!build_with_chromium) {
      configs -= [ "//build/config:sysroot_runtime_libraries" ]
    }

    configs += [ "../../build:openscreen_include_dirs" ]
  }
}
