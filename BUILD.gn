# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# All compilable non-test targets in the repository (both executables and
# source_sets).
group("gn_all") {
  deps = [
    "//api",
    "//base",
    "//discovery/mdns:embedder_demo",
    "//platform",
    "//sample:hello",
    "//third_party/abseil",
    "//third_party/chromium_quic",
    "//third_party/chromium_quic:demo_client",
    "//third_party/chromium_quic:demo_server",
    "//third_party/tinycbor",
    "//tools/cddl",
  ]
}

if (current_os == "mac") {
  source_set("demo") {
  }
} else {
  # TODO(btolsch): Darwin linker has deprecated -m, which handles the multiple
  # definition error.  Until the boringssl/mDNSResponder conflict is fixed
  # (which appears to be exactly the same code in both projects), the demo is
  # excluded on mac.
  executable("demo") {
    sources = [
      "//demo/demo.cc",
    ]

    # TODO(btolsch): Handles MD5_* conflict between boringssl and mDNSResponder
    # temporarily.
    ldflags = [ "-Wl,-z,muldefs" ]
    deps = [
      "//api:api_with_chromium_quic",
      "//base",
      "//discovery/mdns",
      "//platform",
    ]
  }
}

executable("unittests") {
  testonly = true
  deps = [
    "//api:api_unittests",
    "//base:base_unittests",
    "//discovery/mdns:mdns_unittests",
    "//msgs:unittests",
    "//sample:hello_unittests",
    "//third_party/abseil",
    "//third_party/googletest:gtest_main",
  ]
}
