# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build_overrides/build.gni")
import("osp/build/config/services.gni")

# All compilable non-test targets in the repository (both executables and
# source_sets).
group("gn_all") {
  deps = [
    "cast/common:certificate",
    "cast/common:channel",
    "cast/sender:channel",
    "discovery:dnssd",
    "discovery:mdns",
    "osp",
    "osp/msgs",
    "platform",
    "platform:default_logger",
    "streaming/cast:receiver",
    "streaming/cast:sender",
    "third_party/abseil",
    "third_party/boringssl",
    "third_party/jsoncpp",
    "third_party/tinycbor",
    "tools/cddl",
    "util",
  ]

  if (use_mdns_responder) {
    deps += [ "osp/impl/discovery/mdns:mdns_demo" ]
  }

  if (use_chromium_quic) {
    deps += [
      "third_party/chromium_quic",
      "third_party/chromium_quic:quic_demo_client",
      "third_party/chromium_quic:quic_demo_server",
    ]
  }

  if (use_chromium_quic && use_mdns_responder) {
    deps += [ "osp:osp_demo" ]
  }

  if (!build_with_chromium) {
    deps += [
      "third_party/protobuf:protoc",
      "third_party/zlib",
    ]
  }
}

source_set("openscreen_unittests_all") {
  testonly = true
  public_deps = [
    "cast/common:unittests",
    "cast/sender:unittests",
    "discovery:unittests",
    "osp:unittests",
    "osp/msgs:unittests",
    "platform:unittests",
    "streaming/cast:unittests",
    "third_party/abseil",
    "third_party/googletest:gtest_main",
    "util:unittests",
  ]

  if (use_mdns_responder) {
    public_deps += [
      "osp/impl/discovery/mdns:unittests",

      # Currently this target only includes mDNS tests.
      "osp/impl/testing:unittests",
    ]
  }
}

if (!build_with_chromium) {
  executable("openscreen_unittests") {
    testonly = true
    deps = [
      ":openscreen_unittests_all",
    ]
  }
}
