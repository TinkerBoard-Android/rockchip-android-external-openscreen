# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build_overrides/build.gni")

source_set("platform") {
  sources = [
    "api/event_waiter.cc",
    "api/event_waiter.h",
    "api/logging.h",
    "api/logging_util.cc",
    "api/network_interface.cc",
    "api/network_interface.h",
    "api/network_runner.h",
    "api/network_waiter.h",
    "api/scoped_wake_lock.cc",
    "api/scoped_wake_lock.h",
    "api/socket.h",
    "api/task_runner.h",
    "api/time.h",
    "api/tls_socket.h",
    "api/tls_socket_creds.h",
    "api/udp_socket.cc",
    "api/udp_socket.h",
    "api/upd_read_callback.h",
    "impl/ssl_context.cc",
    "impl/ssl_context.h",
    "impl/time.cc",
  ]

  public_deps = [
    "../osp_base",
    "../third_party/abseil",
    "../third_party/boringssl",
  ]

  allow_circular_includes_from = [ "../osp_base" ]

  configs += [ "../build:allow_build_from_embedder" ]

  if (!build_with_chromium) {
    sources += [
      "impl/event_loop.cc",
      "impl/event_loop.h",
      "impl/network_reader.cc",
      "impl/network_reader.h",
      "impl/network_runner.cc",
      "impl/network_runner.h",
      "impl/task_runner_impl.cc",
      "impl/task_runner_impl.h",
    ]

    if (is_linux) {
      sources += [ "impl/network_interface_linux.cc" ]
    } else if (is_mac) {
      defines = [
        # Required, to use the new IPv6 Sockets options introduced by RFC 3542.
        "__APPLE_USE_RFC_3542",
      ]

      sources += [ "impl/network_interface_mac.cc" ]
    }

    if (is_posix) {
      sources += [
        "impl/event_waiter_posix.cc",
        "impl/network_waiter_posix.cc",
        "impl/network_waiter_posix.h",
        "impl/scoped_wake_lock_posix.cc",
        "impl/scoped_wake_lock_posix.h",
        "impl/udp_socket_posix.cc",
        "impl/udp_socket_posix.h",
      ]
    }

    deps = [
      ":default_logger",
    ]
  }
}

source_set("test") {
  testonly = true
  sources = [
    "test/fake_clock.cc",
    "test/fake_clock.h",
    "test/fake_task_runner.cc",
    "test/fake_task_runner.h",
    "test/mock_udp_socket.cc",
    "test/mock_udp_socket.h",
  ]

  configs += [ "../build:allow_build_from_embedder" ]

  deps = [
    ":platform",
    "../third_party/googletest:gmock",
  ]
}

source_set("default_logger") {
  sources = [
    "impl/logging.cc",
  ]

  if (is_posix) {
    sources += [ "impl/log_initializer_posix.cc" ]
  }

  configs += [ "../build:allow_build_from_embedder" ]

  deps = [
    "../osp_base",
    "../third_party/abseil",
  ]
}

source_set("platform_unittests") {
  testonly = true

  sources = [
    "api/time_unittest.cc",
    "api/udp_socket_unittest.cc",
    "impl/ssl_context_unittest.cc",
  ]

  # The unit tests in impl/ assume the standalone implementation is being used.
  # Exclude them if an embedder is providing the implementation.
  if (!build_with_chromium) {
    sources += [
      "impl/network_reader_unittest.cc",
      "impl/task_runner_impl_unittest.cc",
      "impl/time_unittest.cc",
    ]
    if (is_posix) {
      sources += [ "impl/network_waiter_posix_unittest.cc" ]
    }
  }

  deps = [
    ":platform",
    "../third_party/googletest:gmock",
    "../third_party/googletest:gtest",
  ]

  configs += [ "../build:allow_build_from_embedder" ]
}
