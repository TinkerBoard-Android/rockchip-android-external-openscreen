# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

declare_args() {
  # Enable DCHECKs to be compiled in, even if it's not a debug build.
  dcheck_always_on = false

  # Enable address sanitizer.
  is_asan = false
}

config("openscreen_code") {
  cflags = [
    "-Wall",
    "-Werror",
  ]

  if (dcheck_always_on) {
    defines = [ "DCHECK_ALWAYS_ON" ]
  }
}

config("default_optimization") {
  if (is_debug) {
    cflags = [ "-O0" ]
  } else {
    cflags = [ "-O2" ]
  }
}

config("symbol_visibility_hidden") {
  cflags = [ "-fvisibility=hidden" ]
}

config("symbol_visibility_default") {
  cflags = [ "-fvisibility=default" ]
}

config("default_sanitizers") {
  cflags = []
  ldflags = []
  if (is_asan) {
    cflags += [ "-fsanitize=address" ]
    ldflags += [ "-fsanitize=address" ]
  }
}
